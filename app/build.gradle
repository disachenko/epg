apply plugin: 'com.android.application'

def properties = new Properties()
file("../build.properties").withInputStream {
    stream -> properties.load(stream)
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.noriginmedia.epg"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'Integer', 'CONNECTION_TIMEOUT', properties.getProperty("CONNECTION_TIMEOUT")
        buildConfigField 'Integer', 'READ_TIMEOUT', properties.getProperty("READ_TIMEOUT")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'BACKEND_HOST', properties.getProperty("RELEASE_BACKEND_HOST")
        }
        debug {
            minifyEnabled false
            buildConfigField 'String', 'BACKEND_HOST', properties.getProperty("DEBUG_BACKEND_HOST")
        }
    }

    dexOptions {
        javaMaxHeapSize "6g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def supportVersion = "26.1.0"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"

//    dependency injection
    def daggerVersion = '2.13'
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"

//    view binding
    def butterKnifeVersion = "8.8.1"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"

//    networking
    def retrofitVersion = '2.3.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

//    json converter
    def loganSquareVersion = '1.3.6'
    annotationProcessor "com.bluelinelabs:logansquare-compiler:$loganSquareVersion"
    implementation "com.bluelinelabs:logansquare:$loganSquareVersion"
    implementation 'com.github.aurae.retrofit2:converter-logansquare:1.4.1'

    //images loading
    def glideVersion = '4.3.1'
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
}
